plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

allprojects {
    apply plugin: 'java'
    group = 'com.kbskyblock'
    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenCentral()
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://jitpack.io' }
    }

    java {
        def javaVersion = JavaVersion.toVersion(project.findProperty('targetJavaVersion') ?: 17)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release.set(17)
    }
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
    }

    shadowJar {
        archiveClassifier.set('')
        mergeServiceFiles()
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.SF'
    }
}